// >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// Coupled plastic-damage model (non-associated plastic flow) model based on:
//    * http://tfel.sourceforge.net/MohrCoulomb.html
//    * https://doi.org/10.1016/j.compstruc.2016.11.010
//    * https://doi.org/10.1016/0045-7949(94)00339-5
//    * https://doi.org/10.1051/meca/2018043
// <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<

@DSL Implicit;
@Author Simon Raude;
@Date 08/10/2022;
@Behaviour MCBigoniMiniKHD;

@Algorithm NewtonRaphson;
// @Algorithm NewtonRaphson_NumericalJacobian;
// @CompareToNumericalJacobian true;
// @PerturbationValueForNumericalJacobianComputation 1.e-10;

@Theta 1.; 
@Epsilon 1.e-14;

// >>>>>>>>>>>>>>>>>>>>>
// 1. Material Constants
// <<<<<<<<<<<<<<<<<<<<<

// Young modulus (>0, Pa)
@MaterialProperty stress young;
young.setGlossaryName("YoungModulus");

// Poisson ratio (-1<..<0.5, -)
@MaterialProperty real nu;
nu.setGlossaryName("PoissonRatio");

// Friction angle (>0, °)
@MaterialProperty real phi;
phi.setEntryName("FrictionAngle");

// Dilation angle (>0, °)
@MaterialProperty real psi;
psi.setEntryName("DilationAngle");

// Cohesion (>0, Pa)
@MaterialProperty stress coh;
coh.setEntryName("Cohesion");

// Controls the intersection of the hyperbolic branch of the plastic criterion with hydrostatic axis (0<..<=C/tan(phi), Pa)
@MaterialProperty real at;
at.setEntryName("TensionCutOff");

// Damage Strength (>0, Pa)
@MaterialProperty stress Yc;
Yc.setEntryName("DamageStrength");

// Exponent of the denominator of the damage function (0<..<=1, -)
@MaterialProperty real nD;
nD.setEntryName("DExpoDamageFunc");

// Reducing factor in the expression of the kinematic tensor (>=1, -)
@MaterialProperty real qD;
qD.setEntryName("QDamageFactor");

// >>>>>>>>>>>>>>>>>>
// 2. Local Variables
// <<<<<<<<<<<<<<<<<<

@LocalVariable bool b[2];                 // Boolean b[0] for plasticity condition, b[1] for damage condition
@LocalVariable real local_zero_tolerance; // Local zero tolerance
@LocalVariable real pi;                   // Real pi=3.14....
@LocalVariable real phir;                 // Friction angle in radians
@LocalVariable real psir;                 // Dilation angle in radians
@LocalVariable real thetaFm;              // Plastic criterion: Lode angle for which the radius of the MC criterion reach a minimum
@LocalVariable real thetaGm;              // Plastic flow function: Lode angle for which the radius of the MC flow function reach a minimum
@LocalVariable real KFm;                  // Value of the function KF(theta) for Theta=thetaFm
@LocalVariable real KGm;                  // Value of the function KG(theta) for Theta=thetaGm
@LocalVariable stress aG;                 // at*tan(phi)/tan(psi) (in the plastic flow function)
@LocalVariable real bK;                   // Homogenisation factor for the bulk modulus in the expression of the tensor for the kinematical hardening
@LocalVariable real bMu;                  // Homogenisation factor for the shear modulus in the expression of the tensor for the kinematical hardening
@LocalVariable real alpc;                 // Critical value for the damage variable ~1 but <1
@LocalVariable real rD;                   // Regularisation coefficient for the damage function fh
@LocalVariable Stensor4 Ce;               // Elastic stiffness tensor
@LocalVariable Stensor4 Ckin;             // Reduced elastic stiffness tensor for the kinematical hardening
@LocalVariable stress lambda;             // First Lame modulus
@LocalVariable stress mu;                 // Shear modulus
@LocalVariable stress ks;                 // Bulk modulus
@LocalVariable Stensor nGtmp;             // Local variable which is actually the plastic flow direction used to update the auxiliary variable gap

// >>>>>>>>>>>>>>>>>
// 3. State Variable
// <<<<<<<<<<<<<<<<<

@StateVariable StrainStensor epl; // Plastic strain tensor
@StateVariable real lam;          // Plastic multiplier
@StateVariable real alp;          // Damage variable in [0,alpc]

epl.setGlossaryName("PlasticStrain");
lam.setEntryName("PlastMultiplier");
alp.setEntryName("DamageVariable");

// >>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// 4. Auxilliary state variables
// <<<<<<<<<<<<<<<<<<<<<<<<<<<<<

@AuxiliaryStateVariable real niter;    // Number of Newton iterations
@AuxiliaryStateVariable real ip;       // Plastic index
@AuxiliaryStateVariable strain evp;    // Volumetric plastic strain
@AuxiliaryStateVariable strain gap;    // Equivalent plastic strain
@AuxiliaryStateVariable real inst;     // Time
@AuxiliaryStateVariable real domain;   // Domain
@AuxiliaryStateVariable stress XeqASV; // Equivalent shear stress associated to plastic strain
@AuxiliaryStateVariable stress XmASV;  // Mean stress associated to plastic strain
@AuxiliaryStateVariable stress YASV;   // Damage stress

niter.setEntryName("LocalNewtonIter");
ip.setEntryName("PlasticIndex");
evp.setEntryName("PVolumStrain");
gap.setEntryName("EquiPlastStrain");
inst.setEntryName("Time");
domain.setEntryName("PDDomain");
XeqASV.setEntryName("EquiPlastStress");
XmASV.setEntryName("MeanPlastStress");
YASV.setEntryName("DamageStress");

// >>>>>>>>>>>>>>>>>>>>>>>>
// 5. Init. local variables
// <<<<<<<<<<<<<<<<<<<<<<<<

@Includes{
	#include"TFEL/Material/Lame.hxx"
}

@InitLocalVariables{
	using namespace tfel::material::lame;
	local_zero_tolerance=real(1e-12);
	// >>> Convert the FrictionAngle and the DilationAngle to radians
	pi=4.*atan(1.);
	phir=phi*pi/180.;
	psir=psi*pi/180.;
	// >>> Compute KFm and KGm
	thetaFm=atan(-sin(phir)/sqrt(3.));
	thetaGm=atan(-sin(psir)/sqrt(3.));
    KFm=(cos(thetaFm)-sin(phir)*sin(thetaFm)/sqrt(3.));
    KGm=(cos(thetaGm)-sin(psir)*sin(thetaGm)/sqrt(3.));
	// >>> Compute aG
    aG=at*tan(phir)/tan(psir);
	// >>> Parameters for the kinematic tensor
	bK=16./9.*(1.-nu*nu)/(1.-2.*nu);
	bMu=32./45.*(1.-nu)*(5.-nu)/(2.-nu);
	// >>> Numerical parameters for damage evolution
	alpc=.995;
	rD=1.e-4;
	// >>> Compute the elastic moduli, the Hooke tensor, and the kinematic tensor
	lambda=computeLambda(young,nu);
	mu=computeMu(young,nu);
	ks=lambda+2.*mu/3.;
	Ce=3.*ks*Stensor4::J()+2.*mu*Stensor4::K();
	Ckin=(3.*ks*Stensor4::J()/bK+2.*mu*Stensor4::K()/bMu)/qD;
	// >>> Initialise elastic strain (for cases with initial stresses without displacements, e.g. tunnels)
	if (inst==real(0) && lam==real(0)) {
        const Stensor4 Se=invert(Ce);
        eel=(Se*sig);
    }
}

// >>>>>>>>>>>>>>>>>>>>>
// 6. Elastic Prediction
// <<<<<<<<<<<<<<<<<<<<<

@Predictor{
	
	// >>> 6.1 Compute the elastic trial stress
	StressStensor sigel(lambda*trace(eel+deto)*Stensor::Id()+2.*mu*(eel+deto));
	
	// >>> 6.2 Compute the damage function and the tensor Hkin for the kinematical hardening
	const real fh=(1.-alp)*(1.-alp)/pow(alp+rD,nD);
	const Stensor4 Hkin=fh*Ckin;

	// >>> 6.3 Compute the thermo. force associated with the plastic strain
	const StressStensor Xel=sigel-(Hkin*epl);
	const StressStensor Xdel=deviator(Xel);
	const stress J2Xel=.5*(Xdel|Xdel);
	const stress I1Xel=trace(Xel);
	
	// >>> 6.4 Plastic MC criterion - condition for plasticity
	const stress RFel=sqrt(J2Xel*KFm*KFm+at*at*sin(phir)*sin(phir));
	b[0]=RFel+I1Xel*sin(phir)/3.-coh*cos(phir)>real(0);

}

// >>>>>>>>>>>>>>>>>>>>>>>>>>>
// 7. Update the Stress Tensor
// <<<<<<<<<<<<<<<<<<<<<<<<<<<

@ComputeStress{
	sig=lambda*trace(eel)*StrainStensor::Id()+2.*mu*eel;
}

// >>>>>>>>>>>>>>>>>>>>>>>>>>>>
// 8. Plastic-Damage correction
// <<<<<<<<<<<<<<<<<<<<<<<<<<<<

@Integrator{

	// >>> 8.1 Integration when alp>=alpc - perfect plasticity
	if (b[0] && alp>=alpc){

		// >>>>>> 8.1.1 Compute the "kinematic tensor"
		const real fh=(1.-alpc)*(1.-alpc)/pow(alpc+rD,nD);
		const Stensor4 Hkin=fh*Ckin;

		// >>>>>> 8.1.2 Compute the thermo. force associated with the plastic strain
		const StressStensor X=sig-(Hkin*(epl+depl));
		const StressStensor Xd=deviator(X);
		const stress J2X=.5*(Xd|Xd);
		const stress I1X=trace(X);
		
		// >>>>>> 8.1.3 Compute the plastic criterion
		const stress RF=sqrt(J2X*KFm*KFm+at*at*sin(phir)*sin(phir)); // For the plastic criterion
		const stress RG=sqrt(J2X*KGm*KGm+at*aG*sin(psir)*sin(psir)); // For the plastic flow function
		const stress F=RF+I1X*sin(phir)/3.-coh*cos(phir);            // The plastic criterion

		// >>>>>> 8.1.4 Compute the plastic flow direction
		const real dG_dI1X=sin(psir)/3.;
		const real dG_dJ2X=.5*KGm*KGm/max(RG,local_zero_tolerance);
		const Stensor nG=dG_dI1X*Stensor::Id()+dG_dJ2X*Xd;
		nGtmp=nG;

		// >>>>>> 8.1.5 Equations to be solved
		feel=deel-deto+dlam*nG;
		fepl=depl-dlam*nG;
		flam=F/young;
		falp=dalp;
		
		// >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> Comment this block if "@Algorithm NewtonRaphson_NumericalJacobian" >>>>>>>>>>>>>>>>
		
		// >>>>>> 8.1.6 Jacobian
		
		// >>>>>>>>> 8.1.6.1 Some tools
		// >>>>>>>>>>>> Compute dnG_dsig
		const Stensor4 dX_dsig=Stensor4::Id();
		const real dG_ddJ2X=-.5*KGm*KGm*dG_dJ2X/max(RG,local_zero_tolerance)/max(RG,local_zero_tolerance);
		const Stensor4 dnG_dX=dG_ddJ2X*(Xd^Xd)+dG_dJ2X*Stensor4::K();
		const Stensor4 dnG_dsig=(dnG_dX*dX_dsig);
		// >>>>>>>>>>>> Compute dnG_ddepl
		const Stensor4 dX_ddepl=-Hkin;
		const Stensor4 dnG_ddepl=(dnG_dX*dX_ddepl);
		// >>>>>>>>>>>> Compute dF_dsig
		const real dF_dI1X=sin(phir)/3.;
		const real dF_dJ2X=.5*KFm*KFm/max(RF,local_zero_tolerance);
		const Stensor dF_dX=dF_dI1X*Stensor::Id()+dF_dJ2X*Xd;
		const Stensor dF_dsig=(dF_dX|dX_dsig);
		// >>>>>>>>>>>> Compute dF_ddepl
		const Stensor dF_ddepl=(dF_dX|dX_ddepl);
		
		// >>>>>>>>> 8.1.6.2 First line
		dfeel_ddeel=Stensor4::Id()+dlam*(dnG_dsig*Ce);
		dfeel_ddepl=dlam*dnG_ddepl;
		dfeel_ddlam=nG;
		dfeel_ddalp=Stensor(real(0));

		// >>>>>>>>> 8.1.6.3 Second line
		dfepl_ddeel=-dlam*(dnG_dsig*Ce);
		dfepl_ddepl=Stensor4::Id()-dlam*dnG_ddepl;
		dfepl_ddlam=-nG;
		dfepl_ddalp=Stensor(real(0));

		// >>>>>>>>> 8.1.6.4 Third line
		dflam_ddeel=(dF_dsig|Ce)/young;
		dflam_ddepl=dF_ddepl/young;
		dflam_ddlam=real(0);
		dflam_ddalp=real(0);
		
		// >>>>>>>>> 8.1.5.5 Last line
		dfalp_ddeel=Stensor(real(0));
		dfalp_ddepl=Stensor(real(0));
		dfalp_ddlam=real(0);
		dfalp_ddalp=real(1);
		
		// <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
		
	}

	// >>> 8.2 Integration when b[0]=True and alp<alpc - test for the damage criterion
	if (b[0] && alp<alpc){

		// >>>>>> 8.2.1 Compute the "kinematic tensor" Hkin
		const real fh=(1.-alp)*(1.-alp)/pow(alp+rD,nD);
		const real dfh_dalp=(1.-alp)*(alp*nD-2.*alp-2.*rD-nD)/pow(alp+rD,nD+1.);
		const Stensor4 Hkin=fh*Ckin;
		const Stensor4 dHkin_dalp=dfh_dalp*Ckin;

		// >>>>>> 8.2.2 Compute the thermo. forces associated with the plastic strain and the damage variable
		const StressStensor X=sig-(Hkin*(epl+depl));
		const stress Y=-.5*((epl+depl)|dHkin_dalp)|(epl+depl);
		const StressStensor Xd=deviator(X);
		const stress J2X=.5*(Xd|Xd);
		const stress I1X=trace(X);
		
		// >>>>>> 8.2.3 Compute the plastic criterion
		const stress RF=sqrt(J2X*KFm*KFm+at*at*sin(phir)*sin(phir)); // For the plastic criterion
		const stress RG=sqrt(J2X*KGm*KGm+at*aG*sin(psir)*sin(psir)); // For the plastic flow function
		const stress F=RF+I1X*sin(phir)/3.-coh*cos(phir);            // The plastic criterion

		// >>>>>> 8.2.4 Compute the plastic flow direction
		const real dG_dI1X=sin(psir)/3.;
		const real dG_dJ2X=.5*KGm*KGm/max(RG,local_zero_tolerance);
		const Stensor nG=dG_dI1X*Stensor::Id()+dG_dJ2X*Xd;
		nGtmp=nG;

		// >>>>>> 8.2.5 Compute the damage criterion
		b[1]=Y-Yc>real(0);

		// >>>>>> 8.2.6 Equations to be solved
		feel=deel-deto+dlam*nG;
		fepl=depl-dlam*nG;
		flam=F/young;
		falp=dalp;
		
		// >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> Comment this block if "@Algorithm NewtonRaphson_NumericalJacobian" >>>>>>>>>>>>>>>>
		
		// >>>>>> 8.2.7 Jacobian

		// >>>>>>>>> 8.2.7.1 Some tools
		// >>>>>>>>>>>> Compute dnG_dsig
		const Stensor4 dX_dsig=Stensor4::Id();
		const real dG_ddJ2X=-.5*KGm*KGm*dG_dJ2X/max(RG,local_zero_tolerance)/max(RG,local_zero_tolerance);
		const Stensor4 dnG_dX=dG_ddJ2X*(Xd^Xd)+dG_dJ2X*Stensor4::K();
		const Stensor4 dnG_dsig=(dnG_dX*dX_dsig);
		// >>>>>>>>>>>> Compute dnG_ddepl
		const Stensor4 dX_ddepl=-Hkin;
		const Stensor4 dnG_ddepl=(dnG_dX*dX_ddepl);
		// >>>>>>>>>>>> Compute dF_dsig
		const real dF_dI1X=sin(phir)/3.;
		const real dF_dJ2X=.5*KFm*KFm/max(RF,local_zero_tolerance);
		const Stensor dF_dX=dF_dI1X*Stensor::Id()+dF_dJ2X*Xd;
		const Stensor dF_dsig=(dF_dX|dX_dsig);
		// >>>>>>>>>>>> Compute dF_ddepl
		const Stensor dF_ddepl=(dF_dX|dX_ddepl);

		// >>>>>>>>> 8.2.7.2 First line
		dfeel_ddeel=Stensor4::Id()+dlam*(dnG_dsig*Ce);
		dfeel_ddepl=dlam*dnG_ddepl;
		dfeel_ddlam=nG;
		dfeel_ddalp=Stensor(real(0));

		// >>>>>>>>> 8.2.7.3 Second line
		dfepl_ddeel=-dlam*(dnG_dsig*Ce);
		dfepl_ddepl=Stensor4::Id()-dlam*dnG_ddepl;
		dfepl_ddlam=-nG;
		dfepl_ddalp=Stensor(real(0));

		// >>>>>>>>> 8.2.7.4 Third line
		dflam_ddeel=(dF_dsig|Ce)/young;
		dflam_ddepl=dF_ddepl/young;
		dflam_ddlam=real(0);
		dflam_ddalp=real(0);
		
		// >>>>>>>>> 8.2.7.5 Last line
		dfalp_ddeel=Stensor(real(0));
		dfalp_ddepl=Stensor(real(0));
		dfalp_ddlam=real(0);
		dfalp_ddalp=real(1);
		
		// <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
		
	}

	// >>> 8.3 Integration when b[0]=True, b[1]=True, and alp<alpc
	if (b[0] && b[1] && alp<alpc){

		// >>>>>> 8.3.1 Compute the "kinematic tensor" Hkin
		const real fh=(1.-alp-dalp)*(1.-alp-dalp)/pow(alp+dalp+rD,nD);
		const real dfh_dalp=(1.-alp-dalp)*((alp+dalp)*nD-2.*(alp+dalp)-2.*rD-nD)/pow(alp+dalp+rD,nD+1.);
		const Stensor4 Hkin=fh*Ckin;
		const Stensor4 dHkin_dalp=dfh_dalp*Ckin;

		// >>>>>> 8.3.2 Compute the thermo. forces associated with the plastic strain and the damage variable
		const StressStensor X=sig-(Hkin*(epl+depl));
		const stress Y=-.5*((epl+depl)|dHkin_dalp)|(epl+depl);
		const StressStensor Xd=deviator(X);
		const stress J2X=.5*(Xd|Xd);
		const stress I1X=trace(X);
		
		// >>>>>> 8.3.3 Compute the plastic criterion
		const stress RF=sqrt(J2X*KFm*KFm+at*at*sin(phir)*sin(phir)); // For the plastic criterion
		const stress RG=sqrt(J2X*KGm*KGm+at*aG*sin(psir)*sin(psir)); // For the plastic flow function
		const stress F=RF+I1X*sin(phir)/3.-coh*cos(phir);            // The plastic criterion

		// >>>>>> 8.3.4 Compute the plastic flow direction
		const real dG_dI1X=sin(psir)/3.;
		const real dG_dJ2X=.5*KGm*KGm/max(RG,local_zero_tolerance);
		const Stensor nG=dG_dI1X*Stensor::Id()+dG_dJ2X*Xd;
		nGtmp=nG;

		// >>>>>> 8.3.5 Compute the damage criterion
		const stress Fd=Y-Yc;
		
		// >>>>>> 8.3.6 Equations to be solved
		feel=deel-deto+dlam*nG;
		fepl=depl-dlam*nG;
		flam=F/young;
		falp=Fd/young;

		// >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> Comment this block if "@Algorithm NewtonRaphson_NumericalJacobian" >>>>>>>>>>>>>>>>
		
		// >>>>>> 8.3.7 Jacobian

		// >>>>>>>>> 8.3.7.1 Some tools
		// >>>>>>>>>>>> Compute dnG_dsig
		const Stensor4 dX_dsig=Stensor4::Id();
		const real dG_ddJ2X=-.5*KGm*KGm*dG_dJ2X/max(RG,local_zero_tolerance)/max(RG,local_zero_tolerance);
		const Stensor4 dnG_dX=dG_ddJ2X*(Xd^Xd)+dG_dJ2X*Stensor4::K();
		const Stensor4 dnG_dsig=(dnG_dX*dX_dsig);
		// >>>>>>>>>>>> Compute dnG_ddepl
		const Stensor4 dX_ddepl=-Hkin;
		const Stensor4 dnG_ddepl=(dnG_dX*dX_ddepl);
		// >>>>>>>>>>>> Compute dnG_ddalp
		const Stensor4 dHkin_ddalp=dHkin_dalp;
		const StressStensor dX_ddalp=-(dHkin_ddalp*(epl+depl));
		const Stensor dnG_ddalp=(dnG_dX*dX_ddalp);
		// >>>>>>>>>>>> Compute dF_dsig
		const real dF_dI1X=sin(phir)/3.;
		const real dF_dJ2X=.5*KFm*KFm/max(RF,local_zero_tolerance);
		const Stensor dF_dX=dF_dI1X*Stensor::Id()+dF_dJ2X*Xd;
		const Stensor dF_dsig=(dF_dX|dX_dsig);
		// >>>>>>>>>>>> Compute dF_ddepl
		const Stensor dF_ddepl=(dF_dX|dX_ddepl);
		// >>>>>>>>>>>> Compute dF_ddalp
		const real dF_ddalp=(dF_dX|dX_ddalp);
		// >>>>>>>>>>>> Compute dFd_dY
		const real dFd_dY=real(1);
		// >>>>>>>>>>>> Compute dY_ddepl
		const Stensor dY_ddepl=-(dHkin_dalp*(epl+depl));
		// >>>>>>>>>>>> Compute dY_ddalp
		const real d2fh_ddalp_dalp=((alp+dalp)*(alp+dalp)*(nD*(nD-3.)+2.)+2.*(alp+dalp)*(-2.*rD*(nD-1.)-nD*(nD-1.))+2.*rD*(rD+2.*nD)+nD*(nD+1.))/pow(alp+dalp+rD,nD+2.);
		const Stensor4 d2Hkin_ddalp_dalp=d2fh_ddalp_dalp*Ckin;
		const stress dY_ddalp=-.5*((epl+depl)|d2Hkin_ddalp_dalp)|(epl+depl);
		
		// >>>>>>>>> 8.3.7.2 First line
		dfeel_ddeel=Stensor4::Id()+dlam*(dnG_dsig*Ce);
		dfeel_ddepl=dlam*dnG_ddepl;
		dfeel_ddlam=nG;
		dfeel_ddalp=dlam*dnG_ddalp;

		// >>>>>>>>> 8.3.7.3 Second line
		dfepl_ddeel=-dlam*(dnG_dsig*Ce);
		dfepl_ddepl=Stensor4::Id()-dlam*dnG_ddepl;
		dfepl_ddlam=-nG;
		dfepl_ddalp=-dlam*dnG_ddalp;

		// >>>>>>>>> 8.3.7.4 Third line
		dflam_ddeel=(dF_dsig|Ce)/young;
		dflam_ddepl=dF_ddepl/young;
		dflam_ddlam=real(0);
		dflam_ddalp=dF_ddalp/young;
		
		// >>>>>>>>> 8.3.6.5 Last line
		dfalp_ddeel=Stensor(real(0));
		dfalp_ddepl=(dFd_dY*dY_ddepl)/young;
		dfalp_ddlam=real(0);
		dfalp_ddalp=(dFd_dY*dY_ddalp)/young;
		
		// <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
		
	}

	// >>> 8.3 Elasticity
	if (not b[0]){

		feel=deel-deto;
		fepl=depl;
		flam=dlam;
		falp=dalp;

	}
}

// >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// 9. Update Auxilliary State Variables
// <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<

@UpdateAuxiliaryStateVariables{
	const real fh=(1.-alp)*(1.-alp)/pow(alp+rD,nD);
	const real dfh_dalp=(1.-alp)*(alp*nD-2.*alp-2.*rD-nD)/pow(alp+rD,nD+1.);
	const Stensor4 Hkin=fh*Ckin;
	const Stensor4 dHkin_dalp=dfh_dalp*Ckin;
	const StressStensor XASV=sig-(Hkin*(epl));
	XeqASV=sigmaeq(XASV);
	XmASV=trace(XASV)/3.;
	YASV=-.5*((epl|dHkin_dalp)|epl);
	niter=iter;
	inst+=dt;
	if (dlam>0) {
		ip=1;
		evp+=dlam*sin(psir);
		gap+=dlam*sqrt(2.*deviator(nGtmp)|deviator(nGtmp)/3.);
	} else {
		ip=0;
	}
	if (b[0] && alp>=alpc) {
		domain=3;
	} else if (b[0] && alp<alpc) {
		domain=1;
	} else if (b[0] && b[1] && alp<alpc) {
		domain=2;
	} else {
		domain=0;
	}
}

// >>>>>>>>>>>>>>>>>>>>
// 10. Tangent Operator
// <<<<<<<<<<<<<<<<<<<<

@TangentOperator{
	if((smt==ELASTIC)||(smt==SECANTOPERATOR)) {
		computeElasticStiffness<N,Type>::exe(Dt,lambda,mu);
	}
	else if (smt==CONSISTENTTANGENTOPERATOR) {
		Stensor4 Je;
		getPartialJacobianInvert(Je);
		Dt=Ce*Je;
	} 
	else {
		return false;
	}
}
